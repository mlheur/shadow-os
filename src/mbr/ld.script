/* hard constants, chosen by IBM PC engineers */
_start                  = 0x0000;
mbr_partition_table     = 0x01BE;
mbr_signature           = 0x01FE;
mbr_heap                = 0x7000; /* through 0x79FF */

/* Main linker script */
ENTRY(_start)
SECTIONS {
    .text : {
        . = _start;
        ../../objs/_start.o(.text)

        . = mbr_partition_table;
        ../../objs/mbr_partition_table.o(.text)
        sizeof_partition_table = . - mbr_partition_table;

        . = mbr_signature;
        ../../objs/mbr_signature.o(.text)
        sizeof_MBR = . - _start;
    }

    .bss : {
        . = mbr_heap;
        ../../objs/_start.o(.bss)
    }
    
    /DISCARD/ : { *(.data) }
}
OUTPUT_ARCH(i386)
OUTPUT_FORMAT(binary)

/* calculated constants from hard constants and computed symbols */
longof_MBR              = sizeof_MBR / 4;
sizeof_partition_entry  = sizeof_partition_table / 4;
sizeof_MBR_magic        = _start + sizeof_MBR - mbr_signature;
aligned_partition_table = mbr_partition_table + sizeof_MBR_magic;
